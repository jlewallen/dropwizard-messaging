apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'
mainClassName = 'com.page5of4.dropwizard.activemq.example.publisher.Main'

configurations {
    capsule
}

run {
    systemProperty "dropwizard.config", "build/resources/main/activemq-publisher.yml"
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.0'
    compile "com.google.guava:guava:14.0.1"
    compile 'com.page5of4.dropwizard.activemq:dropwizard-activemq-server:0.1.0'
    compile 'org.apache.camel:camel-core:2.13.0'
    compile 'org.apache.camel:camel-jms:2.13.0'
    compile 'org.apache.camel:camel-jackson:2.13.0'
    compile 'org.apache.camel:camel-gson:2.13.0'

    capsule 'co.paralleluniverse:capsule:0.4.0'
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "dropwizard-activemq-sample-publisher.jar"

    from jar
    from { configurations.runtime }

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }

    manifest {
        attributes(
                'Main-Class': 'Capsule',
                'Application-Class': mainClassName,
                'Application-Version': version,
                'Min-Java-Version': '1.8.0',
                'JVM-Args': run.jvmArgs.join(' '),
                'System-Properties': run.systemProperties.collect { k, v -> "$k=$v" }.join(' '),
        )
    }
}

