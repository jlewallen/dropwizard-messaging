apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

sourceCompatibility = '1.8'
mainClassName = 'com.page5of4.dropwizard.activemq.example.subscriber.Main'

configurations {
    capsule
}

run {
    systemProperty "dropwizard.config", "build/resources/main/activemq-subscriber.yml"
    args "server", "build/resources/main/activemq-subscriber.yml"
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.0'
    compile 'com.hubspot.dropwizard:dropwizard-guice:0.6.2'
    compile 'org.jolokia:jolokia-core:1.2.1'

    compile project(':dropwizard-activemq')
    compile project(':codon-dropwizard')
    compile project(':codon-discovery')
    compile project(':codon-discovery-activemq')
    // Move messages to their own jar
    compile project(':example-publisher')

    compile 'com.page5of4.dropwizard:dropwizard-eureka-client:0.1.0-SNAPSHOT'
    compile 'com.page5of4.dropwizard:dropwizard-zookeeper:0.1.0-SNAPSHOT'

    capsule 'co.paralleluniverse:capsule:0.4.0'
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "dropwizard-activemq-sample-subscriber.jar"

    from jar
    from { configurations.runtime }

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }

    manifest {
        attributes(
                'Main-Class': 'Capsule',
                'Application-Class': mainClassName,
                'Application-Version': version,
                'Min-Java-Version': '1.8.0',
                'JVM-Args': run.jvmArgs.join(' '),
                'System-Properties': run.systemProperties.collect { k, v -> "$k=$v" }.join(' '),
        )
    }
}

