apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

sourceCompatibility = '1.8'
mainClassName = 'com.page5of4.dropwizard.activemq.Main'

configurations {
    capsule
}

run {
    systemProperty "dropwizard.config", "build/resources/main/activemq.yml"
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.0'
    compile "com.google.guava:guava:14.0.1"
    compile 'org.apache.activemq:activemq-broker:5.9.1'
    compile 'org.apache.activemq:activemq-kahadb-store:5.9.1'
    compile 'org.apache.activemq:activemq-client:5.9.1'

    capsule 'co.paralleluniverse:capsule:0.4.0'
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "dropwizard-activemq.jar"

    from jar
    from { configurations.runtime }

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }

    manifest {
        attributes(
                'Main-Class': 'Capsule',
                'Application-Class': mainClassName,
                'Application-Version': version,
                'Min-Java-Version': '1.8.0',
                'JVM-Args': run.jvmArgs.join(' '),
                'System-Properties': run.systemProperties.collect { k, v -> "$k=$v" }.join(' '),
        )
    }
}

